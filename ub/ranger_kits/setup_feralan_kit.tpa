DEFINE_ACTION_FUNCTION setup_feralan_kit
BEGIN
  /////////////////
  /////////////////
  // Feralan Kit //
  /////////////////
  /////////////////

  /* AUTHOR   ~andyr@gibberlings3.net~ */

  COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    SET "armor_patch" = 0
    SET "other_patch" = 0
    READ_BYTE 0x1C "type"
    READ_BYTE 0x1E "bard"
    READ_BYTE 0x21 "druid"
    READ_BYTE 0x29 "shapeshifter"
    WHILE ("%armor_patch%" = 0) AND
          (("%shapeshifter%"  BAND 0b00010000) = 0b00000000) AND // if usable by shapeshifters
          (("%type%" = 2) OR // Armor
          ("%type%" = 60) OR // Leather Armor
          ("%type%" = 61) OR // Studded Leather Armor
          ("%type%" = 62) OR // Chain Mail
          ("%type%" = 63) OR // Splint Mail
          ("%type%" = 64) OR // Half plate
          ("%type%" = 65) OR // Full plate
          ("%type%" = 67) OR // Robes
          ("%type%" = 66))   // Hide armor
    BEGIN
      WRITE_BYTE 0x29 ("%shapeshifter%" BOR 0b00010000) // making sure that all armor is unusable by shapeshifters
      SET "armor_patch" = 1
    END
    WHILE ("%other_patch%" = 0) AND
          (("%shapeshifter%" BAND 0b00010000) = 0b00010000) AND // and unusable by shapeshifters
          (("%druid%" BAND 0b01000000) = 0b01000000) AND NOT    // and unusable by druids
          (("%type%" = 2)  OR // Armor
           ("%type%" = 60) OR // Leather Armor
           ("%type%" = 61) OR // Studded Leather Armor
           ("%type%" = 62) OR // Chain Mail
           ("%type%" = 63) OR // Splint Mail
           ("%type%" = 64) OR // Half plate
           ("%type%" = 65) OR // Full plate
           ("%type%" = 67) OR // Robes
           ("%type%" = 66))   // Hide armor
    BEGIN
      WRITE_BYTE 0x29 ("%shapeshifter%" BAND 0b11101111) // make usable by shapeshifters
      SET "other_patch" = 1
    END
    END
  BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~SPCL331.SPL~ ~override/A#fera01.SPL~
  WRITE_LONG 0x9e 10
  WRITE_LONG 0xce 10

  COPY_EXISTING ~SPCL321.SPL~ ~override/A#fera02.SPL~
  SAY NAME1 @86

  COPY ~%MOD_FOLDER%/ranger_kits/A#fera03.SPL~ ~override/A#fera03.SPL~
  SAY NAME1 @87

  COPY ~%MOD_FOLDER%/ranger_kits/A#fera01.CRE~ ~override/A#fera01.CRE~
  SAY NAME1 @88
  SAY NAME2 @88
  COPY ~%MOD_FOLDER%/ranger_kits/A#fera02.CRE~ ~override/A#fera02.CRE~
  SAY NAME1 @89
  SAY NAME2 @89
  COPY ~%MOD_FOLDER%/ranger_kits/A#fera03.CRE~ ~override/A#fera03.CRE~
  SAY NAME1 @90
  SAY NAME2 @90
  COPY ~%MOD_FOLDER%/ranger_kits/A#fera04.CRE~ ~override/A#fera04.CRE~
  SAY NAME1 @91
  SAY NAME2 @91

  COPY ~%MOD_FOLDER%/ranger_kits/A#fera01.EFF~ ~override/A#fera01.EFF~
  COPY ~%MOD_FOLDER%/ranger_kits/A#fera02.EFF~ ~override/A#fera02.EFF~
  COPY ~%MOD_FOLDER%/ranger_kits/A#fera03.EFF~ ~override/A#fera03.EFF~
  COPY ~%MOD_FOLDER%/ranger_kits/A#fera04.EFF~ ~override/A#fera04.EFF~

  COPY ~%MOD_FOLDER%/ranger_kits/A#feral.2DA~ ~override/A#feral.2DA~

  ADD_KIT ~A#FERALAN~
  // CLASWEAP.2DA Weapons class can use.
  ~A#FERALAN		1	1	1	1	1	1	1	1~
  // WEAPPROF.2DA, starting with line 3. Proficiencies allowed.
  ~A#FERALAN 1 1 2 2 2 1 2 2 1 1 1 2 1 1 1 2 2 2 2 1 1 2 2 1 2 2 2 2 2 2 2 3 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5~
  // ABCLASRQ.2DA Stat requirements.
  ~A#FERALAN	13	13	14	0	14	0~
  // ABCLSMOD.2DA Stat modifiers.
  ~A#FERALAN		0	0	0	0	0	-2~
  // ABDCDSRQ.2DA Dualclass requirements.
  ~A#FERALAN		17	17	0	0	17	0~
  // ABDCSCRQ.2DA Dualclass requirements.
  ~A#FERALAN		15	15	0	0	15	0~
  // ALIGNMNT.2DA Permitted alignments.
  ~A#FERALAN		0	0	0	1	0	0	1	0	0~
  // DUALCLAS.2DA Dualclass classes.
  ~A#FERALAN		0	1	0	0	0	0~
  // Abilities 2DA file
  ~override/A#feral.2DA~
  // KITTABLE.2DA What sort of kit it is. R = ranger.
  ~K_R_H K_R_HE K_R_E~
  // KITLIST.2DA  Number is the class it is a kit for. Ranger = 12.
  ~0x10000000	12~
  // LUABBR.2DA HLAs.
  ~ra0~
  // 25STWEAP.2DA ToB starting equipment.
  ~* * HELM19 BAG27 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC14 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
  // Strings for lower, mixed and help descriptions. First two are names for ingame, the third is the description.
  SAY @92
  SAY @93
  SAY @94

  LAUNCH_ACTION_FUNCTION fl#add_kit_ee
    STR_VAR
    kit_name	= "A#FERALAN"
    hpclass	= "hpwar"
  END
END	// setup_feralan_kit


