DEFINE_ACTION_FUNCTION setup_suna_seni_valygar
BEGIN
  ////////////////////////////////////////
  ////////////////////////////////////////
  // The Suna Seni/Valygar Relationship //
  ////////////////////////////////////////
  ////////////////////////////////////////

  COPY ~%mod_root%/ubnull.itm~ ~override/ubsuna.xxx~ //null file to identify this component

  ADD_JOURNAL EXISTING TITLE (#74367) @211 @213 USING ~%tra_loc%/%LANGUAGE%/ubdialog.tra~
  ADD_JOURNAL EXISTING TITLE (#74343) @322 USING ~%tra_loc%/%LANGUAGE%/ubdialog.tra~

  COMPILE ~%mod_root%/suna/CCSUNA.baf~                        /* Suna Seni's script in the Copper Coronet */
          ~%mod_root%/suna/ppsuna.baf~                        /* Suna Seni's script in Spellhold */
          ~%mod_root%/suna/Resuna.baf~                        /* Suna Seni's script in the Slums */
          ~%mod_root%/suna/u!cut41g.baf~                      /* Display Suna in Spellhold Cutscene */
          ~%mod_root%/suna/ppsuna.d~                          /* Suna Seni's dialog in Spellhold */
          ~%mod_root%/suna/SUNA.D~                            /* Suna Seni's dialog in Athkatla */
          ~%mod_root%/suna/u!suna.d~
          ~%mod_root%/suna/u!1101.baf~                        /* Removes Valygar from his cabin if not given the quest */
  USING   ~%tra_loc%/%s/ubdialog.tra~

  EXTEND_TOP    ~ar1101.bcs~  ~override/u!1101.bcs~   /* Removes Valygar from his cabin if not given the quest */
  EXTEND_TOP    ~ar0041.bcs~  ~%mod_root%/suna/u!sunare.baf~ EVALUATE_BUFFER /* Removes Suna Seni from the Random Encounters */
  EXTEND_TOP    ~ar0042.bcs~  ~%mod_root%/suna/u!sunare.baf~ EVALUATE_BUFFER /* Removes Suna Seni from the Random Encounters */
  EXTEND_TOP    ~ar0043.bcs~  ~%mod_root%/suna/u!sunare.baf~ EVALUATE_BUFFER /* Removes Suna Seni from the Random Encounters */
  EXTEND_TOP    ~ar0044.bcs~  ~%mod_root%/suna/u!sunare.baf~ EVALUATE_BUFFER /* Removes Suna Seni from the Random Encounters */
  EXTEND_TOP    ~ar0045.bcs~  ~%mod_root%/suna/u!sunare.baf~ EVALUATE_BUFFER /* Removes Suna Seni from the Random Encounters */
  EXTEND_TOP    ~ar0046.bcs~  ~%mod_root%/suna/u!sunare.baf~ EVALUATE_BUFFER /* Removes Suna Seni from the Random Encounters */
  EXTEND_TOP    ~ar1516.bcs~  ~%mod_root%/suna/u!1516.baf~    /* Spawns Suna in Spellhold */
  EXTEND_TOP    ~ar0300.bcs~  ~%mod_root%/suna/u!0300.baf~    /* Spawns Suna in the Docks */
  EXTEND_TOP    ~ppjon.bcs~   ~%mod_root%/suna/u!ppjon.baf~   /* Determines whether or not to spawn Suna in the Spellhold cutscene */
  EXTEND_TOP    ~baldur.bcs~  ~%mod_root%/suna/u!baldur.baf~  /* Begins Valygar's dialog shortly after Suna's death */ 
  EXTEND_TOP    ~ar0406.bcs~  ~%mod_root%/suna/u!0406.baf~    /* Spawns Suna at the Copper Coronet */ /* Kulyok - other mods may add looping scripts to CC and baldur, got a report, so EXTEND_TOP */

  COPY_EXISTING ~AR1101.are~  ~override/AR1101.are~   /* Valygar's Cabin */
  WRITE_ASCII   ~0x94~        ~AR1101~

  COPY ~%mod_root%/suna/ppsuna.cre~   ~override/ppsuna.cre~
       ~%mod_root%/suna/resuna.cre~   ~override/resuna.cre~
       ~%mod_root%/suna/suna.cre~     ~override/suna.cre~     
  SAY NAME1 @21
  SAY NAME2 @21
  REPLACE_TEXTUALLY CASE_INSENSITIVE ~sw1h27~ ~sw1h08~

  /* Places Arbane's Sword on Eldarin */
  COPY_EXISTING reeldar.cre override
    ADD_CRE_ITEM sw1h27 #0 #0 #0 NONE WEAPON2
  BUT_ONLY
END	// setup_suna_seni_valygar


